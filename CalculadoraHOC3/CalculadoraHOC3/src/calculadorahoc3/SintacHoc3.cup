// Calculadora HOC2
package calculadorahoc3;
import java_cup.runtime.*;
import java.io.FileReader;

class AnalizadorSintactico;

parser code {:
    HOC3JFrame frmInterfaz;
    Float variables[] = new Float[26];

    public void report_error(String mensaje, Object info){
        StringBuilder m = new StringBuilder("Error");
        if(info instanceof java_cup.runtime.Symbol){
            java_cup.runtime.Symbol s = (java_cup.runtime.Symbol) info;
            if(s.left >= 0){
                m.append(" en la linea "+(s.left+1));
                if(s.right >= 0)
                    m.append(", columna "+(s.right+1));
            }
        }
        m.append(" : "+mensaje);
        System.err.println(m);
    }

    public void report_fatal_error(String mssg, Object info){
        report_error(mssg, info);
        // System.exit(1);
    }

    public Float findFunction(Integer index, Float dato){
        Float resultado = new Float(0);
        switch(index){
            case 1:
                resultado = new Float(Math.sin(dato));
            break;
            case 2:
                resultado = new Float(Math.cos(dato));
            break;
            case 3:
                resultado = new Float(Math.atan(dato));
            break;
            case 4:
                resultado = new Float(Math.log(dato));
            break;
            case 5:
                resultado = new Float(Math.log10(dato));
            break;
            case 6:
                resultado = new Float(Math.exp(dato));
            break;
            case 7:
                resultado = new Float(Math.sqrt(dato));
            break;
            case 8:
                resultado = new Float((int) dato);
            break;
            case 9:
                resultado = new Float(Math.abs(dato));
            break;
            default:
                resultado = new Float(0);
            break;
        }
        return resultado;
    }

:}

terminal opSUMA, opRESTA, opMULT, opDIV, ASIG;
terminal ParIzq, ParDer, ENTER, SEMICOLON;
terminal Float NUM;
terminal Float CONSTS;
terminal Integer VAR;
terminal Integer BLTIN;
terminal UMINUS, POW;

non terminal list;
non terminal Float asgn, expr;

// Precedencias
precedence right ASIG;
precedence left opSUMA, opRESTA;
precedence left opMULT, opDIV;
precedence left UMINUS;
precedence left POW;

list    ::=
        | ENTER
        | list expr:e SEMICOLON             {: frmInterfaz.PonerTextSintac(Float.toString(e)+"\n"); :}
        | list expr:e SEMICOLON ENTER       {: frmInterfaz.PonerTextSintac(Float.toString(e)+"\n"); :}
        ;

asgn    ::= VAR:index ASIG expr:e           {: variables[index]=e; RESULT=e; :}
        ;

expr    ::= NUM:valNum                      {: RESULT=valNum; :}
        | VAR:index                         {: RESULT=variables[index]; :}
        | asgn:valAsgn                      {: RESULT=valAsgn; :}
        | expr:e1 opSUMA expr:e2            {: RESULT=e1+e2; :}
        | expr:e1 opRESTA expr:e2           {: RESULT=e1-e2; :}
        | expr:e1 opMULT expr:e2            {: RESULT=e1*e2; :}
        | expr:e1 opDIV expr:e2             {: RESULT=e1/e2; :}
        | ParIzq expr:e ParDer              {: RESULT=e; :}
        | opRESTA expr:e                    {: RESULT=-e; :} %prec UMINUS
        | expr:e1 POW expr:e2               {: RESULT=(float) Math.pow(e1,e2); :}
        | CONSTS:co                         {: RESULT=co; :}
        | BLTIN:fun ParIzq expr:e ParDer    {: Float elec=findFunction(fun,e); RESULT=elec; :}
        ;